/*
 * ------------------------------------------
 *
 *  HIGH-PERFORMANCE INTEGRATED MODELLING SYSTEM (HiPIMS)
 *  Luke S. Smith and Qiuhua Liang
 *  luke@smith.ac
 *
 *  School of Civil Engineering & Geosciences
 *  Newcastle University
 * 
 * ------------------------------------------
 *  This code is licensed under GPLv3. See LICENCE
 *  for more information.
 * ------------------------------------------
 *  INERTIAL SCHEME
 * ------------------------------------------
 *  Implementation of the simplified inertial
 *  scheme
 * ------------------------------------------
 *
 */

 #define Cfacweir 2.95245

/*
 *  Calculate everything without using LDS caching
 */
__kernel REQD_WG_SIZE_FULL_TS
void ine_cacheDisabled ( 
			__constant	cl_double *  				dTimestep,					// Timestep
			__global	cl_double const * restrict	dBedElevation,				// Bed elevation
			__global	cl_double4 *  			pCellStateSrc,					// Current cell state data
			__global	cl_double4 *  			pCellStateDst,					// Current cell state data
			__global	cl_double const * restrict	dManning,					// Manning values
			__global	sUsePolini const * restrict	pUsePoleni,					// Switch to use Poleni
			__global	cl_double const * restrict	pOpt_zxmax,					// 	
			__global	cl_double const * restrict	pOpt_cx,					// 	
			__global	cl_double const * restrict	pOpt_zymax,					// 	
			__global	cl_double const * restrict	pOpt_cy						// 	

		)
{

	// Identify the cell we're reconstructing (no overlap)
	__private cl_long					lIdxX			= get_global_id(0);
	__private cl_long					lIdxY			= get_global_id(1);
	__private cl_ulong					ulIdx, ulIdxNeig;
	__private cl_uchar					ucDirection;
	
	ulIdx = getCellID(lIdxX, lIdxY);

	// Don't bother if we've gone beyond the domain bounds
	if ( lIdxX > DOMAIN_COLS - 1 || 
		 lIdxY > DOMAIN_ROWS - 1 || 
		 lIdxX < 0 || 
		 lIdxY < 0 ) 
		return;

	// TimeStep
	__private cl_double		dLclTimestep	= *dTimestep;
	// Change of water level
	__private cl_double		dDeltaFSL;
	// Bed elevations (opt_z)
	__private cl_double		dCellBedElev,dNeigBedElevN,dNeigBedElevE,dNeigBedElevS,dNeigBedElevW;
	// Manning coefficients
	__private cl_double		dManningCoef, pNeigManN, pNeigManE, pNeigManS,pNeigManW;
	// Water surface level (opt_s)
	__private cl_double4	pCellData,pNeigDataN,pNeigDataE,pNeigDataS,pNeigDataW;					// opt_s, opt_s_max, Qx, Qy
	// Discharges
	__private cl_double		dDischarge[4];															// Qn, Qe, Qs, Qw
	// Flags to check scheme to use (main scheme or polini)
	__private sUsePolini	pUsePoleniFlags;
	__private bool			usePoleniN, usePoleniE, usePoleniS, usePoleniW;
	__private cl_double		dOpt_zxmax,dOpt_zymax,dOpt_znxmax,dOpt_znymax;
	__private cl_double		dOpt_cx, dOpt_cy, dOpt_cnx,dOpt_cny;


	// Counter for dry values surrounding cell
	__private cl_uchar		ucDryCount		= 0;
	

	// Also don't bother if we've gone beyond the total simulation time
	if ( dLclTimestep <= 0.0 )
		return;

	// Load cell data
	dCellBedElev		= dBedElevation[ ulIdx ];
	pCellData			= pCellStateSrc[ ulIdx ];
	dManningCoef		= dManning[ ulIdx ];
	dOpt_cx				= pOpt_cx	[ ulIdx ];
	dOpt_zymax			= pOpt_zymax[ ulIdx ];
	dOpt_cy				= pOpt_cy	[ ulIdx ];


	
	pUsePoleniFlags		= pUsePoleni[ ulIdx ];
	usePoleniN			= pUsePoleniFlags.usePoliniN;
	usePoleniE			= pUsePoleniFlags.usePoliniE;
	usePoleniS			= pUsePoleniFlags.usePoliniS;
	usePoleniW			= pUsePoleniFlags.usePoliniW;

	// Cell disabled?
	if ( pCellData.y <= -9999.0 || pCellData.x == -9999.0 )
	{
		pCellStateDst[ ulIdx ] = pCellData;
		return;
	}

	ucDirection = DOMAIN_DIR_N;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevN	= dBedElevation [ ulIdxNeig ];
	pNeigDataN		= pCellStateSrc	[ ulIdxNeig ];
	pNeigManN		= dManning [ ulIdxNeig ];
	
	ucDirection = DOMAIN_DIR_E;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevE	= dBedElevation [ ulIdxNeig ];
	pNeigDataE		= pCellStateSrc	[ ulIdxNeig ];
	pNeigManE		= dManning [ ulIdxNeig ];

	ucDirection = DOMAIN_DIR_S;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevS	= dBedElevation [ ulIdxNeig ];
	pNeigDataS		= pCellStateSrc	[ ulIdxNeig ];
	pNeigManS		= dManning [ ulIdxNeig ];
	dOpt_znymax		= pOpt_zymax[ ulIdxNeig ];
	dOpt_cny		= pOpt_cy[ ulIdxNeig ];

	ucDirection = DOMAIN_DIR_W;
	ulIdxNeig = getNeighbourByIndices(lIdxX, lIdxY, ucDirection);
	dNeigBedElevW	= dBedElevation [ ulIdxNeig ];
	pNeigDataW		= pCellStateSrc	[ ulIdxNeig ];
	pNeigManW		= dManning [ ulIdxNeig ];
	dOpt_znxmax		= pOpt_zxmax[ ulIdxNeig ];
	dOpt_cnx		= pOpt_cx[ ulIdxNeig ];

	if ( pCellData.x  - dCellBedElev  < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataN.x - dNeigBedElevN < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataE.x - dNeigBedElevE < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataS.x - dNeigBedElevS < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataW.x - dNeigBedElevW < VERY_SMALL ) ucDryCount++;

	// All neighbours are dry? Don't bother calculating
	if ( ucDryCount >= 5 ) return;

	bool debug = false;

	if (!usePoleniN){
	dDischarge[ DOMAIN_DIR_N ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pNeigDataN.w,
		pNeigDataN.x,
		dNeigBedElevN,
		pCellData.x,
		dCellBedElev
	);
				}else{
					dDischarge[ DOMAIN_DIR_N ] = poleni_Solver(
						dLclTimestep,
						dManningCoef,
						pNeigManN,
						pCellData.x,
						pNeigDataN.x,
						dCellBedElev,
						dNeigBedElevN,
						dOpt_zymax,
						dOpt_cy,
						debug
					);
				}
	if (!usePoleniE){
	dDischarge[ DOMAIN_DIR_E ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pNeigDataE.z,
		pNeigDataE.x,
		dNeigBedElevE,
		pCellData.x,
		dCellBedElev
	);
				}else{
					dDischarge[ DOMAIN_DIR_E ] = poleni_Solver(
						dLclTimestep,
						dManningCoef,
						pNeigManE,
						pCellData.x,
						pNeigDataE.x,
						dCellBedElev,
						dNeigBedElevE,
						dOpt_zxmax,
						dOpt_cx,
						debug
					);
				}
	if (!usePoleniS){
	dDischarge[ DOMAIN_DIR_S ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pCellData.w,
		pCellData.x,
		dCellBedElev,
		pNeigDataS.x,
		dNeigBedElevS
	);
				}else{
					dDischarge[ DOMAIN_DIR_S ] = -1.0*poleni_Solver(
						dLclTimestep,
						dManningCoef,
						pNeigManS,
						pCellData.x,
						pNeigDataS.x,
						dCellBedElev,
						dNeigBedElevS,
						dOpt_znymax,
						dOpt_cny,
						debug
					);
				}
	if (!usePoleniW){
	dDischarge[ DOMAIN_DIR_W ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pCellData.z,
		pCellData.x,
		dCellBedElev,
		pNeigDataW.x,
		dNeigBedElevW
	);
				}else{
					dDischarge[ DOMAIN_DIR_W ] = -1.0*poleni_Solver(
						dLclTimestep,
						dManningCoef,
						pNeigManW,
						pCellData.x,
						pNeigDataW.x,
						dCellBedElev,
						dNeigBedElevW,
						dOpt_znxmax,
						dOpt_cnx,
						debug
						);
					}
	


	if (ulIdx == 587){

		//printf("Postion: X: %f Y:%f \n",lIdxX*1.0 , lIdxY*1.0 );
		//printf("opt_z %f opt_h %f opt_s %f ",dCellBedElev, (pCellData.x-dCellBedElev), pCellData.x);
		//printf("pN %f pE %f pS %f pW %f ",usePoleniN ? 1.0 : 0.0, usePoleniE ? 1.0 : 0.0, usePoleniS ? 1.0 : 0.0, usePoleniW ? 1.0 : 0.0);
		//printf("dOpt_zxmax %f dOpt_cx %f dOpt_zymax %f dOpt_cy %f ",dOpt_zxmax, dOpt_cx, dOpt_zymax, dOpt_cy);
		//printf("dN %f dE %f dS %f dW %f", dDischarge[ DOMAIN_DIR_N ],dDischarge[ DOMAIN_DIR_E ],dDischarge[ DOMAIN_DIR_S ],dDischarge[ DOMAIN_DIR_W ]);
		//printf("\n");
	}


	if(lIdxX == DOMAIN_COLS - 1 || dNeigBedElevE == -9999.0){
		dDischarge[DOMAIN_DIR_E] = 0.0;
	} 
	if( lIdxY == DOMAIN_ROWS - 1 || dNeigBedElevN == -9999.0) {
		dDischarge[DOMAIN_DIR_N] = 0.0;
	}
	if( lIdxX == 0 || dNeigBedElevW == -9999.0) {
		dDischarge[DOMAIN_DIR_W] = 0.0;
	}
	if( lIdxY == 0 || dNeigBedElevS == -9999.0) {
		dDischarge[DOMAIN_DIR_S] = 0.0;
	}

	pCellData.z		= dDischarge[DOMAIN_DIR_W];
	pCellData.w		= dDischarge[DOMAIN_DIR_S];

	// Calculation of change values per timestep and spatial dimension
	dDeltaFSL		= ( dDischarge[DOMAIN_DIR_E] - dDischarge[DOMAIN_DIR_W] + 
					    dDischarge[DOMAIN_DIR_N] - dDischarge[DOMAIN_DIR_S] )/DOMAIN_DELTAY;

	// Update the flow state
	pCellData.x		= pCellData.x + dLclTimestep * dDeltaFSL;

	// New max FSL?
	if ( pCellData.x > pCellData.y )
		pCellData.y = pCellData.x;

	// Crazy low depths?
	if ( pCellData.x - dCellBedElev < VERY_SMALL )
		pCellData.x = dCellBedElev;

	// Commit to global memory
	pCellStateDst[ ulIdx ] = pCellData;
}

/*
 *  Calculate everything by also using LDS caching
 */
__kernel REQD_WG_SIZE_FULL_TS
void ine_cacheEnabled ( 
			__constant	cl_double *  				dTimestep,						// Timestep
			__global	cl_double const * restrict	dBedElevation,					// Bed elevation
			__global	cl_double4 *  			pCellStateSrc,					// Current cell state data
			__global	cl_double4 *  			pCellStateDst,					// Current cell state data
			__global	cl_double const * restrict	dManning						// Manning values
		)
{
	__local   cl_double4				lpCellState[ INE_DIM1 ][ INE_DIM2 ];		// Current cell state data (cache)

	// Identify the cell we're reconstructing (no overlap)
	__private cl_double					dLclTimestep;
	__private cl_long					lIdxX			= get_global_id(0) - get_group_id(0) * 2;
	__private cl_long					lIdxY			= get_global_id(1) - get_group_id(1) * 2;
	__private cl_long					lLocalX			= get_local_id(0);
	__private cl_long					lLocalY			= get_local_id(1);
	__private cl_ulong					lLocalSizeX		= get_local_size(0);
	__private cl_ulong					lLocalSizeY		= get_local_size(1);
	__private cl_double4				pCellData;
	__private cl_double					dManningCoef;
	__private cl_double					dCellBedElev;
	__private cl_ulong					ulIdx;

	if ( lIdxX > DOMAIN_COLS - 1 || 
		 lIdxY > DOMAIN_ROWS - 1 || 
		 lIdxX < 0 || 
		 lIdxY < 0 )
	{
		// Ideally we'd just exit the function here, but then we wont reach the barrier
		lIdxX = max((cl_long)0,min((cl_long)(DOMAIN_COLS - 1),lIdxX));
		lIdxY = max((cl_long)0,min((cl_long)(DOMAIN_ROWS - 1),lIdxY));
	}

	ulIdx = getCellID(lIdxX, lIdxY);

	// The max FSL is substituted with the bed elevation, thereby reducing LDS consumption
	dLclTimestep							= *dTimestep;
	pCellData								= pCellStateSrc[ ulIdx ];
	dCellBedElev							= dBedElevation[ ulIdx ];
	dManningCoef							= dManning[ ulIdx ];
	lpCellState[ lLocalX ][ lLocalY ]		= pCellData;
	lpCellState[ lLocalX ][ lLocalY ].y		= dCellBedElev;

	barrier( CLK_LOCAL_MEM_FENCE );

	// Cell disabled?
	if ( pCellData.y <= -9999.0 || pCellData.x == -9999.0 )
	{
		pCellStateDst[ ulIdx ] = pCellData;
		return;
	}

	// Don't bother if we've gone beyond the domain bounds
	if ( lIdxX >= DOMAIN_COLS - 1 || 
		 lIdxY >= DOMAIN_ROWS - 1 || 
		 lIdxX <= 0 || 
		 lIdxY <= 0 ||
		 lLocalX >= lLocalSizeX - 1 ||
		 lLocalY >= lLocalSizeY - 1 ||
		 lLocalX <= 0 ||
		 lLocalY <= 0 ) 
		return;

	__private cl_double		dNeigBedElevN,dNeigBedElevE,dNeigBedElevS,dNeigBedElevW;
	__private cl_double4	pNeigDataN,pNeigDataE,pNeigDataS,pNeigDataW;							// Z, Zb, Qx, Qy
	__private cl_double		dDeltaFSL;																
	__private cl_double		dDischarge[4];															// Qn, Qe, Qs, Qw
	__private cl_uchar		ucDryCount		= 0;
	

	// Also don't bother if we've gone beyond the total simulation time
	if ( dLclTimestep <= 0.0 )
		return;

	pNeigDataW		= lpCellState[ lLocalX - 1 ][ lLocalY ];
	pNeigDataS		= lpCellState[ lLocalX ][ lLocalY - 1 ];
	pNeigDataE		= lpCellState[ lLocalX + 1 ][ lLocalY ];
	pNeigDataN		= lpCellState[ lLocalX ][ lLocalY + 1 ];
	dNeigBedElevW	= pNeigDataW.y;
	dNeigBedElevS	= pNeigDataS.y;
	dNeigBedElevE	= pNeigDataE.y;
	dNeigBedElevN	= pNeigDataN.y;

	// Cell disabled?
	if ( pCellData.y <= -9999.0 || pCellData.x == -9999.0 )
	{
		pCellStateDst[ ulIdx ] = pCellData;
		return;
	}

	if ( pCellData.x  - dCellBedElev  < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataN.x - dNeigBedElevN < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataE.x - dNeigBedElevE < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataS.x - dNeigBedElevS < VERY_SMALL ) ucDryCount++;
	if ( pNeigDataW.x - dNeigBedElevW < VERY_SMALL ) ucDryCount++;

	// All neighbours are dry? Don't bother calculating
	if ( ucDryCount >= 5 ) return;

	// Calculate fluxes
	// -> North
	dDischarge[ DOMAIN_DIR_N ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pNeigDataN.w,
		pNeigDataN.x,
		dNeigBedElevN,
		pCellData.x,
		dCellBedElev
	);
	// -> East
	dDischarge[ DOMAIN_DIR_E ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pNeigDataE.z,
		pNeigDataE.x,
		dNeigBedElevE,
		pCellData.x,
		dCellBedElev
	);
	// -> South
	dDischarge[ DOMAIN_DIR_S ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pCellData.w,
		pCellData.x,
		dCellBedElev,
		pNeigDataS.x,
		dNeigBedElevS
	);
	// -> West
	dDischarge[ DOMAIN_DIR_W ] = calculateInertialFlux(
		dManningCoef,
		dLclTimestep,
		pCellData.z,
		pCellData.x,
		dCellBedElev,
		pNeigDataW.x,
		dNeigBedElevW
	);

	pCellData.z		= dDischarge[DOMAIN_DIR_W];
	pCellData.w		= dDischarge[DOMAIN_DIR_S];

	// Calculation of change values per timestep and spatial dimension
	dDeltaFSL		= ( dDischarge[DOMAIN_DIR_E] - dDischarge[DOMAIN_DIR_W] + 
					    dDischarge[DOMAIN_DIR_N] - dDischarge[DOMAIN_DIR_S] )/DOMAIN_DELTAY;

	// Update the flow state
	pCellData.x		= pCellData.x + dLclTimestep * dDeltaFSL;

	// New max FSL?
	if ( pCellData.x > pCellData.y )
		pCellData.y = pCellData.x;

	// Crazy low depths?
	if ( pCellData.x - dCellBedElev < VERY_SMALL )
		pCellData.x = dCellBedElev;

	// Commit to global memory
	pCellStateDst[ ulIdx ] = pCellData;
}

/*
 *  Calculate the flux using an inertial approximation in terms of volumetric discharge per unit width
 */
cl_double calculateInertialFlux(
		cl_double		dManningCoef,					// Manning coefficient
		cl_double		dTimestep,						// Timestep
		cl_double		dPreviousDischarge,				// Last current discharge
		cl_double		dLevelUpstream,					// Upstream current level
		cl_double		dBedUpstream,					// Upstream bed elevation
		cl_double		dLevelDownstream,				// Downstream current level
		cl_double		dBedDownstream					// Downstream bed elevation
	)
{
	cl_double dDischarge = 0.0;
	cl_double dDepth	 = fmax( dLevelDownstream, dLevelUpstream ) - max( dBedUpstream, dBedDownstream );
	cl_double dSlope	 = ( dLevelDownstream - dLevelUpstream ) / DOMAIN_DELTAX;

	// Inertial 
	dDischarge = ( dPreviousDischarge - ( GRAVITY * dDepth * dTimestep * dSlope ) ) /
			     ( 1.0 + GRAVITY * dDepth * dTimestep * dManningCoef * dManningCoef * fabs( dPreviousDischarge ) /
				   pow( dDepth, 10.0/3.0 ) );

	//Promaides
	//cl_double dDischarge2 = 0.0;
	//cl_double delta_h    = dLevelUpstream - dLevelDownstream;
	//cl_double abs_delta_h	= fabs(delta_h);
	//dDischarge2 = 1.0/dManningCoef * pow(dDepth,5.0/3.0) * delta_h / pow(DOMAIN_DELTAX * abs_delta_h,0.5);
	//printf("Inertail: %f Promaides: %f\n", dDischarge*1000000.0,dDischarge2*1000000.0);

	// Froude number discharge limiter
	if ( dDischarge > 0.0 &&
		 ( ( fabs( dDischarge ) / dDepth ) / sqrt( GRAVITY * dDepth ) ) > FROUDE_LIMIT )
		dDischarge = dDepth * sqrt( GRAVITY * dDepth ) * FROUDE_LIMIT;
	if ( dDischarge < 0.0 &&
		 ( ( fabs( dDischarge ) / dDepth ) / sqrt( GRAVITY * dDepth ) ) > FROUDE_LIMIT )
		dDischarge = 0.0 - dDepth * sqrt( GRAVITY * dDepth ) * FROUDE_LIMIT;

	// Some completely arbitrary other limiter?!
	// This is used in CAESAR-LISFLOOD but I can't for the life of me work out the logic behind it...
	/*
	if ( dDischarge > 0.0 &&
		 ( fabs( dDischarge ) * dTimestep / (cl_double)(DOMAIN_DELTAX) ) > ( dLevelUpstream - dBedUpstream ) / 4.0 )
		dDischarge = ( ( dLevelUpstream - dBedUpstream ) * (cl_double)(DOMAIN_DELTAX) ) / 5.0 / dTimestep;
	if ( dDischarge < 0.0 &&
		 ( fabs( dDischarge ) * dTimestep / (cl_double)(DOMAIN_DELTAX) ) > ( dLevelDownstream - dBedDownstream ) / 4.0 )
		dDischarge = 0.0 - ( ( dLevelDownstream - dBedDownstream ) * (cl_double)(DOMAIN_DELTAX) ) / 5.0 / dTimestep;
	*/

	if ( dDepth < VERY_SMALL )
		dDischarge = 0.0;

	// Done...
	return dDischarge;
}

cl_double poleni_Solver(
	cl_double timestep,		// Current Time-step
	cl_double manning,     	// Manning of Main Cell
	cl_double manning_Neig,	// Manning of Neighbor cell
	cl_double opt_s_Cell,	// Water surface level of Main Cell
	cl_double opt_s_Neig,	// Water surface level of Neighbor cell
	cl_double opt_z_Cell, 	// Bed Elevation of main Cell
	cl_double opt_z_Neig,	// Bed Elevation of Neighbor Cell

	cl_double opt_zxymax,
	cl_double opt_cxy,

	bool debug				// Debug Flag
	)
	{

		cl_double output;
		cl_double dDischarge = 0.0;

		cl_double flow_depth	= opt_s_Cell - opt_zxymax;
		cl_double flow_depth_neigh	=  opt_s_Neig - opt_zxymax;
		cl_double reduction_term;
		
		cl_double option = 0.0;

		//noFlow
		if((flow_depth<=0.0 && flow_depth_neigh<=0.0) || (fabs(flow_depth-flow_depth_neigh)<=0.0)){
			option = 0.5;
			dDischarge = 0.0;
		}else{
			//flow out of this element without submerged weirflow reduction into the neihgbouring element
			if( flow_depth > 0.0 && flow_depth_neigh <= 0.0){				
				 option = 1.0;
				dDischarge = -1.0 * Cfacweir * opt_cxy * pow(flow_depth, (3.0/2.0)) ;
		
			//flow out of the neighbouring element without submerged weirflow reduction into this element
			}else if(flow_depth <= 0.0 && flow_depth_neigh > 0.0){					
				option = 2.0;	
				dDischarge = Cfacweir * opt_cxy * pow(flow_depth_neigh,(3.0/2.0));

			//submerged weirflow with reduction
			}else if(flow_depth > 0.0 && flow_depth_neigh > 0.0){	
				if(flow_depth > flow_depth_neigh){							//flow into the neihgbouring element	
					option = 3.0;		
					reduction_term = (1.0-flow_depth_neigh/flow_depth);		//reduction of the discharge (submerged weirflow)
					if(reduction_term <= 0.000463529){						//replace the ^(1/3) by a fitted arctan-function; at the boundary they have the same values
						option = 3.1;
						dDischarge = -1.0 * Cfacweir * opt_cxy * pow(flow_depth,(3.0/2.0)) * 0.057965266895 * atan(8984.365582471040 * reduction_term);
					}else{
						option = 3.2;
						dDischarge = -1.0 * Cfacweir * opt_cxy * pow(flow_depth,(3.0/2.0)) * pow(reduction_term,(1.0/3.0));
					}
				}
				else{	
					option = 4.0;	
					//flow into this element
					reduction_term = (1.0 - flow_depth/flow_depth_neigh);	//reduction of the discharge (submerged weirflow)
					if(reduction_term <= 0.000463529){						//replace the ^(1/3) by a fitted arctan-function; at the boundary they have the same values
						option = 4.1;	
						dDischarge = Cfacweir * opt_cxy * pow(flow_depth_neigh,(3.0/2.0)) * 0.057965266895 * atan(8984.365582471040 * reduction_term);
					}else{
						option = 4.2;	
						dDischarge = Cfacweir * opt_cxy * pow(flow_depth_neigh,(3.0/2.0)) * pow(reduction_term,(1.0/3.0));
					}
				}
			}
		}
		if (debug){
			printf("option %f \n", option);
			printf("opt_s_Cell %f \n", opt_s_Cell);
			printf("opt_s_Neig %f \n", opt_s_Neig);
			printf("opt_zxymax %f \n", opt_zxymax);
			printf("flow_depth %f \n", flow_depth);
			printf("flow_depth_neigh %f \n", flow_depth_neigh);
			printf("dDischarge %f \n", dDischarge);
			printf("\n");
		}
		output = dDischarge*DOMAIN_DELTAY;

		return output;
	}